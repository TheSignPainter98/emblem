use crate::parser::{
	LexicalError,
	Location,
	lexer::Tok,
	point::Point,
};
use crate::ast::{
    parsed::{
		Attr,
		Attrs,
		Content,
		MultiLineComment,
		MultiLineCommentPart,
		ParsedFile,
		Sugar,
	},
	Par, ParPart, Text
};

grammar<'input>;

pub File: ParsedFile<'input> = {
    FileContent => <>.into(),
};

FileContent: Vec<Par<ParPart<Content<'input>>>> = {
	MaybeSepList<Par, par_break>
}

Par: Par<ParPart<Content<'input>>> = {
	ParPart+ => <>.into(),
}

ParPart: ParPart<Content<'input>> = {
	<MaybeLineContent> "\n" => ParPart::Line(<>),

	<l:@L> <name:CommandName> <attrs:Attrs?> <inline_args:("{" <MaybeLineContent> "}")*> ":" "\n" <trail_head:Indented<FileContent>> <trail_tail:("::" "\n" <Indented<FileContent>>)*> <r:@R> => {
		ParPart::Command(Content::Command {
			name: name.0,
			pluses: name.1,
			attrs,
			inline_args,
			remainder_arg: None,
			trailer_args: {
				let mut args = vec![trail_head];
				args.extend(trail_tail);
				args
			},
			loc: Location::new(&l, &r),
			invocation_loc: name.2,
		})
	},
}

MaybeLineContent: Vec<Content<'input>> = {
	LineContent? => <>.unwrap_or_default()
}

LineContent: Vec<Content<'input>> = {
	LineElement+,
	<mut content:LineElement*> <tail:RemainderCommand> => {
		content.push(tail);
		content
	},
}

RemainderCommand: Content<'input> = {
	<l:@L> <name:CommandName> <attrs:Attrs?> <inline_args:("{" <MaybeLineContent> "}")*> <remainder_arg:(":" <LineContent>)> <r:@R> => Content::Command {
		name: name.0,
		pluses: name.1,
		attrs,
		inline_args,
		remainder_arg: Some(remainder_arg),
		trailer_args: Vec::with_capacity(0),
		loc: Location::new(&l, &r),
		invocation_loc: name.2,
	}
}

CommandName: (Text<'input>, usize, Location<'input>) = {
	<l:@L> <command:command> <r:@R> => (command.0.into(), command.1.into(), Location::new(&l, &r)),
}

LineElement: Content<'input> = {
	<l:@L> <comment:comment>       <r:@R> => Content::Comment{ comment, loc: Location::new(&l, &r) },
	<l:@L> <content:NestedComment> <r:@R> => Content::MultiLineComment{ content, loc: Location::new(&l, &r) },
	<l:@L> <whitespace:whitespace> <r:@R> => Content::Whitespace{ whitespace, loc: Location::new(&l, &r) },
	<l:@L> <word:word>             <r:@R> => Content::Word{ word: word.into(), loc: Location::new(&l, &r) },
	<l:@L> <dash:dash>             <r:@R> => Content::Dash{ dash: dash.into(), loc: Location::new(&l, &r) },
	<l:@L> <glue:glue>             <r:@R> => Content::Glue{ glue: glue.into(), loc: Location::new(&l, &r) },
	<l:@L> <verbatim:verbatim>     <r:@R> => Content::Verbatim{ verbatim, loc: Location::new(&l, &r) },

	EmphSugar,

	<l:@L> <name:CommandName> <attrs:Attrs?> <inline_args:("{" <MaybeLineContent> "}")*> <r:@R> => Content::Command {
		name: name.0,
		pluses: name.1,
		attrs,
		inline_args,
		remainder_arg: None,
		trailer_args: Vec::with_capacity(0),
		loc: Location::new(&l, &r),
		invocation_loc: name.2,
	},
}

EmphSugar: Content<'input> = {
	<l:@L> <delimiter:italic_open> <arg:LineElement+> italic_close <r:@R> => Content::Sugar(
		Sugar::Italic{
			delimiter,
			arg,
			loc: Location::new(&l, &r),
		},
	),
	<l:@L> <delimiter:bold_open> <arg:LineElement+> bold_close <r:@R> => Content::Sugar(
		Sugar::Bold{
			delimiter,
			arg,
			loc: Location::new(&l, &r),
		},
	),
	<l:@L> monospace_open <arg:LineElement+> monospace_close <r:@R> => Content::Sugar(
		Sugar::Monospace{
			arg,
			loc: Location::new(&l, &r),
		},
	),
	<l:@L> smallcaps_open <arg:LineElement+> smallcaps_close <r:@R> => Content::Sugar(
		Sugar::Smallcaps{
			arg,
			loc: Location::new(&l, &r),
		},
	),
	<l:@L> alternate_face_open <arg:LineElement+> alternate_face_close <r:@R> => Content::Sugar(
		Sugar::AlternateFace{
			arg,
			loc: Location::new(&l, &r),
		},
	),
}

Attrs: Attrs<'input> = {
	<l:@L> "[" <attrs:MaybeSepList<Attr, ",">> "]" <r:@R> => Attrs::new(attrs, Location::new(&l, &r)),
}

Attr: Attr<'input> = {
	<l:@L> <attr:named_attr> <r:@R> => Attr::named(attr, Location::new(&l, &r)),
	<l:@L> <attr:unnamed_attr> <r:@R> => Attr::unnamed(attr, Location::new(&l, &r)),
};

NestedComment: MultiLineComment<'input> = {
	"/*" <NestedCommentContent> "*/"
}

NestedCommentContent: MultiLineComment<'input> = {
	<NestedCommentPart*> => MultiLineComment(<>),
}

NestedCommentPart: MultiLineCommentPart<'input> = {
	"\n"          => MultiLineCommentPart::Newline,
	comment       => MultiLineCommentPart::Comment(<>),
	NestedComment => MultiLineCommentPart::Nested(<>),
}

SepList<T,Sep>: Vec<T> = {
	<head:T> <mut tail:MaybeSepList<T,Sep>> => {
		let mut ret = vec![head];
		ret.append(&mut tail);
		ret
	}
}

MaybeSepList<T, Sep>: Vec<T> = {
	<mut init:(<T> Sep)*> <last:T?> => match last {
		None => init,
		Some(e) => {
			init.push(e);
			init
		}
	},
}

Indented<T>: T = {
	indent <MaybeIndented<T>> dedent,
}

MaybeIndented<T>: T = {
	T,
	indent <MaybeIndented<T>> dedent,
}

extern {
	type Location = Point<'input>;
	type Error = Box<LexicalError<'input>>;

	enum Tok<'input> {
		indent               => Tok::Indent,
		dedent               => Tok::Dedent,
		":"                  => Tok::Colon,
		"::"                 => Tok::DoubleColon,
		"{"                  => Tok::LBrace,
		"}"                  => Tok::RBrace,
		command              => Tok::Command(<&'input str>, <usize>),
		italic_open          => Tok::ItalicOpen(<&'input str>),
		bold_open            => Tok::BoldOpen(<&'input str>),
		monospace_open       => Tok::MonospaceOpen(<&'input str>),
		smallcaps_open       => Tok::SmallcapsOpen(<&'input str>),
		alternate_face_open  => Tok::AlternateFaceOpen(<&'input str>),
		italic_close         => Tok::ItalicClose,
		bold_close           => Tok::BoldClose,
		monospace_close      => Tok::MonospaceClose,
		smallcaps_close      => Tok::SmallcapsClose,
		alternate_face_close => Tok::AlternateFaceClose,
		par_break            => Tok::ParBreak,
		word                 => Tok::Word(<&'input str>),
		dash                 => Tok::Dash(<&'input str>),
		glue                 => Tok::Glue(<&'input str>),
		verbatim             => Tok::Verbatim(<&'input str>),
		whitespace           => Tok::Whitespace(<&'input str>),
		"["                  => Tok::LBracket,
		"]"                  => Tok::RBracket,
		","                  => Tok::AttrComma,
		named_attr           => Tok::NamedAttr(<&'input str>),
		unnamed_attr         => Tok::UnnamedAttr(<&'input str>),
		"/*"                 => Tok::NestedCommentOpen,
		"*/"                 => Tok::NestedCommentClose,
		"\n"                 => Tok::Newline,
		comment              => Tok::Comment(<&'input str>),
	}
}
